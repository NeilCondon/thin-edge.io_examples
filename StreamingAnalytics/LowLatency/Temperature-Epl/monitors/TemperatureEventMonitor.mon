using com.apama.aggregates.mean;
/** If average temperature in 10 seconds is greater than 80 C, send a shutdown alert. */

event Temperature {
    float value;
    constant string SUBSCRIBE_CHANNEL := "mqtt:sensors/temperature";
    constant string SEND_CHANNEL := "mqtt:alerts/temperature";
}

event ShutDown {
    string shutdownMessage;
    constant string SEND_CHANNEL := "mqtt:device/operations";
}

monitor TempEventMonitor {
	
    constant float TEN_SEC_INTERVAL := 10.0;
    constant float THRESHOLD_TEMPERATURE := 80.0;
    
    action onload() {
       monitor.subscribe(Temperature.SUBSCRIBE_CHANNEL);
       stream<Temperature> temperatures := all Temperature();
       from temp in temperatures within TEN_SEC_INTERVAL
         select Temperature(mean(temp.value)) as temperature{
           if temperature.value > THRESHOLD_TEMPERATURE {
                log temperature.value.toString() at INFO;
                log "Average temperature " + temperature.value.toString() + " Celsius" + " exceeded threshold temperature " + THRESHOLD_TEMPERATURE.toString() + " Celsius" + " ." + "Sending alert and requesting shutdown ." at WARN;
                send ShutDown("ShutDown ! Temp is high") to ShutDown.SEND_CHANNEL;
                send Temperature(temperature.value) to Temperature.SEND_CHANNEL;
           }
        }
    }
}
