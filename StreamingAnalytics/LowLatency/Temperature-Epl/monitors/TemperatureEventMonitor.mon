using com.apama.aggregates.mean;
/** If average temperature in 5 minutes is greater than 80 C, send a shutdown alert . */

event Temperature {
    float value;
}

event ShutDown {
    string shutdownMessage;
}

monitor TempEventMonitor {
	
    constant float FIVE_MINS_INTERVAL := 300.0;
    constant float THRESHOLD_TEMPERATURE := 80.0;
    
    action onload() {
       monitor.subscribe("mqtt:sensors/temperature");
       monitor.subscribe("mqtt:alerts/temperature");
       stream<Temperature> temperatures := all Temperature();
       from temp in temperatures within FIVE_MINS_INTERVAL
         select Temperature(mean(temp.value)) as temperature{
           if temperature.value > THRESHOLD_TEMPERATURE {
                log temperature.value.toString() at INFO;
                log "Average temperature " + temperature.value.toString() + " Celsius" + " exceeded threshold temperature " + THRESHOLD_TEMPERATURE.toString() + " Celsius" + " ." + "Sending alert and requesting shutdown ." at WARN;
                send ShutDown("ShutDown ! Temp is high") to "mqtt:alerts/temperature";
                send Temperature(temperature.value) to "mqtt:alerts/temperature";
           }
        }
    }
}
